buildscript {
    apply from: "$rootDir/gradle/dependencies.gradle"
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath lib.spring_boot_gradle_plugin
    }
}

plugins {
    id "io.freefair.lombok" version "4.1.6"
}

allprojects {
    defaultTasks 'clean', 'cleanResources', 'build', 'testClasses'

    repositories {
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'idea'
}

subprojects {
    apply plugin: "org.springframework.boot"
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'

    dependencyManagement {
        imports {
            // 插件org.springframework.boot已经包含了版本管理功能,这个声明可以不要
            // mavenBom "org.springframework.boot:spring-boot-starter-parent:2.2.5.RELEASE"
            mavenBom lib.spring_cloud_dependencies
        }
    }
    
    version = '1.0'

    task cleanResources(type: Delete) {
        delete 'build/resources'
    }
    tasks.withType(AbstractCompile) {
        options.encoding = "UTF-8"
    }
    compileJava.dependsOn processResources
    processResources.dependsOn cleanResources

    task("copyJar", type: Copy) {
        from ("$rootDir/$project.name/build/libs") {
            include "**/*.jar"
        }
        into "$rootDir/deploy/lib/"
    }
    task("deleteJar", type: Delete) {
        delete fileTree("$rootDir/deploy/lib/") {
            include "**/*.jar"
        }
    }
    build.finalizedBy copyJar
    clean.finalizedBy deleteJar
}

apply from: "$rootDir/deps.gradle"